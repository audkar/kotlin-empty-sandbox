buildscript {
    ext.kotlin_version = '1.3.71'
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.6.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "com.github.ben-manes.versions" version "0.28.0"
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

wrapper {
    gradleVersion = '6.3'
    //noinspection GroovyAccessibility
    distributionType = Wrapper.DistributionType.ALL
}

def isNonStable = { String version ->
    def stableKeyword = ['FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

dependencyUpdates {
    rejectVersionIf {
        isNonStable(it.candidate.version)
    }
}
